Value LOCAL_INTERFACE (\S+)
Value CHASSIS_ID (\w+?\.\w+?\.\w+?)
Value NEIGHBOR_PORT_ID (\S+)
Value NEIGHBOR_INTERFACE (\S+)
Value NEIGHBOR (\S+)
Value SYSTEM_DESCRIPTION (.*)
Value CAPABILITIES (.*)
Value MANAGEMENT_IP (\S+)
Value VLAN (\d+)

Start
  ^.*LLDP is not enabled -> EOF
  ^Local\s+Intf:\s+${LOCAL_INTERFACE}\s*$$
  ^Chassis\s+id:\s+${CHASSIS_ID}\s*$$
  ^Port\s+id:\s+${NEIGHBOR_PORT_ID}\s*$$
  ^Port\s+Description:\s+${NEIGHBOR_INTERFACE}\s*$$
  ^System\s+Name(?::\s+${NEIGHBOR}|\s+-\s+not\s+advertised)\s*$$
  ^System\s+Description -> GetDescription
  ^Time
  ^System\s+Capabilities
  ^Enabled\s+Capabilities:\s+${CAPABILITIES}\s*$$
  ^Management\s+Addresses
  ^\s+OID
  ^\s+[\d+\.]{8,}
  ^.*IP:\s+${MANAGEMENT_IP}
  ^Auto\s+Negotiation
  ^Physical\s+media
  ^\s+.+\(\S+\)\s*$$
  ^\s*[MEDmed]+
  ^\s+Inventory
  ^\s+Capabilities
  ^\s+Device\s+type
  ^\s+Network\s+Policies
  ^\s+Power\s+requirements
  ^\s+Location
  ^Vlan\s+ID:\s+(?:${VLAN}|-\s+not\s+advertised)\s*$$
  ^\s+\(\S+\)
  ^(?:PoE|\s+Power)
  ^-+ -> Record
  ^\s*$$
  ^\s*Total\s+entries\s+displayed -> Record
  ^\s*$$
  ^.*$$ -> Error

GetDescription
  ^${SYSTEM_DESCRIPTION} -> Start
  ^\s*$$
  ^.*$$ -> Error
